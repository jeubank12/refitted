plugins {
    id 'com.google.devtools.ksp'
    id 'com.android.application'
    id 'com.google.gms.google-services'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

android {
    defaultConfig {
        applicationId "com.litus_animae.refitted"
        minSdkVersion 26
        compileSdk 34
        targetSdkVersion 34
        versionCode 3426003
        versionName "1.2.3-SNAPSHOT"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ksp {
            arg("room.schemaLocation", "$projectDir/schemas".toString())
            arg("room.expandProjection", "true")
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += []
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            ndk { debugSymbolLevel 'FULL' }
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue 'string', "cognito_identity_pool_id", Refitted_IdentityPoolId
            resValue 'string', "firebase_id_source", Refitted_OpenIdSource
            debuggable = false
        }
        minDebug {
            signingConfig signingConfigs.debug
            minifyEnabled true
            shrinkResources true
            ndk { debugSymbolLevel 'FULL' }
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue 'string', "cognito_identity_pool_id", Refitted_IdentityPoolId
            resValue 'string', "firebase_id_source", Refitted_OpenIdSource
            debuggable true
            applicationIdSuffix ".min_debug"
            versionNameSuffix "-MIN-DEBUG"
        }
        debug {
            signingConfig signingConfigs.debug
            resValue 'string', "cognito_identity_pool_id", Refitted_IdentityPoolId
            resValue 'string', "firebase_id_source", Refitted_OpenIdSource
            applicationIdSuffix ".debug"
            versionNameSuffix "-DEBUG"
        }
    }
    buildFeatures {
        dataBinding true
        compose true
        buildConfig true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
        freeCompilerArgs += ["-opt-in=kotlin.RequiresOptIn"]
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.5.14'
    }
    namespace 'com.litus_animae.refitted'
    
    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
    }

    lint {
        checkDependencies true
    }
}

dependencies {

    // this may be used for some sort of error reporting
    // want to replace with crashlytics
    implementation libs.androidx.legacy.support.v4

    implementation libs.androidx.core.runtime

    def room_version = '2.6.1'
    implementation libs.androidx.room.paging
    def paging_version = '3.3.1'
    implementation libs.androidx.paging.common.ktx
    implementation "androidx.paging:paging-compose:$paging_version"

    // UI Tests
    androidTestImplementation platform(libs.androidx.compose.bom)
    androidTestImplementation(libs.androidx.ui.test.junit4)
    debugImplementation(libs.androidx.ui.test.manifest)
    def junit_version = '5.10.3'
    testImplementation libs.junit.jupiter
    testImplementation libs.junit.jupiter.api
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_version"
    testImplementation libs.truth
    androidTestImplementation libs.androidx.runner
    androidTestImplementation libs.androidx.espresso.core

    implementation libs.androidx.room.runtime
    ksp "androidx.room:room-compiler:$room_version"
    implementation libs.androidx.room.ktx

    implementation libs.aws.android.sdk.core
    implementation libs.aws.android.sdk.ddb.mapper

    def hilt_compiler_version = '1.2.0'
    implementation libs.hilt.android
    kapt "com.google.dagger:hilt-android-compiler:$hilt_android_version"
    annotationProcessor "com.google.dagger:hilt-android-compiler:$hilt_android_version"
    implementation libs.androidx.navigation.compose
    implementation libs.androidx.hilt.navigation.compose
    ksp "androidx.hilt:hilt-compiler:$hilt_compiler_version"

    implementation libs.play.services.auth

    implementation platform(libs.firebase.bom)
    implementation libs.firebase.auth
    implementation libs.firebase.firestore
    implementation libs.firebase.analytics

    implementation platform(libs.arrow.stack)
    implementation libs.arrow.core

    def kotlinx_couroutines_version = '1.8.1'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinx_couroutines_version"
    implementation libs.kotlinx.coroutines.play.services

    implementation platform(libs.androidx.compose.bom)
    implementation libs.androidx.ui
    // Tooling support (Previews, etc.)
    implementation libs.androidx.ui.tooling
    // Foundation (Border, Background, Box, Image, Scroll, shapes, animations, etc.)
    implementation libs.androidx.foundation
    // Material Design
    implementation libs.androidx.material
    // Material design icons
    implementation libs.androidx.material.icons.core
    implementation libs.androidx.material.icons.extended
    // Integration with observables
    // Integration with activities
    implementation libs.androidx.activity.compose
    // Animations
    implementation libs.androidx.animation
    // Integration with ViewModels
    def lifecycle_version = '2.8.4'
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycle_version"

    // match androidx.compose-ui version with https://github.com/google/accompanist#compose-versions
    implementation libs.accompanist.adaptive


    implementation libs.androidx.credentials

    // optional - needed for credentials support from play services, for devices running
    // Android 13 and below.
    implementation libs.androidx.credentials.play.services.auth
    implementation libs.googleid
}

repositories {
    mavenCentral()
}
