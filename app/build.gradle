plugins {
    alias libs.plugins.application
    alias libs.plugins.dagger.hilt.android
    alias libs.plugins.dependency.analysis
    alias libs.plugins.compose.compiler
    alias libs.plugins.google.services
    alias libs.plugins.firebase.crashlytics
    alias libs.plugins.jetbrains.kotlin.android
    alias libs.plugins.jetbrains.kotlin.parcelize
    alias libs.plugins.ksp
}

android {
    defaultConfig {
        applicationId "com.litus_animae.refitted"
        minSdkVersion 26
        compileSdk 34
        targetSdkVersion 34
        versionCode 3426008
        versionName "1.2.8-SNAPSHOT"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ksp {
            arg("room.schemaLocation", "$projectDir/schemas".toString())
            arg("room.expandProjection", "true")
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += []
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            ndk { debugSymbolLevel 'FULL' }
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue 'string', "cognito_identity_pool_id", Refitted_IdentityPoolId
            resValue 'string', "firebase_id_source", Refitted_OpenIdSource
            debuggable = false
        }
        debug {
            signingConfig signingConfigs.debug
            resValue 'string', "cognito_identity_pool_id", Refitted_IdentityPoolId
            resValue 'string', "firebase_id_source", Refitted_OpenIdSource
            applicationIdSuffix ".debug"
            versionNameSuffix "-DEBUG"
        }
    }
    buildFeatures {
        dataBinding true
        compose true
        buildConfig true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
        freeCompilerArgs += ["-opt-in=kotlin.RequiresOptIn"]
    }
    namespace 'com.litus_animae.refitted'
    
    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
    }

    lint {
        checkDependencies true
    }
}

dependencies {

    implementation libs.androidx.activity
    implementation libs.androidx.annotation
    implementation libs.androidx.appcompat
    implementation libs.guava
    implementation libs.javax.inject
    implementation libs.androidx.lifecycle.runtime.compose.android

    testImplementation libs.androidx.core.runtime
    implementation libs.androidx.fragment
    implementation libs.androidx.recyclerview

    implementation libs.androidx.room.paging
    implementation libs.androidx.paging.compose
    implementation libs.androidx.paging.runtime

    // UI Tests
    androidTestImplementation platform(libs.androidx.compose.bom)
    androidTestImplementation libs.junit
    debugRuntimeOnly libs.androidx.compose.ui.test.manifest
    testImplementation libs.junit.jupiter.api
    testRuntimeOnly libs.junit.jupiter.engine
    testImplementation libs.truth
    androidTestImplementation libs.androidx.test.runner
    androidTestImplementation libs.androidx.test.monitor

    implementation libs.androidx.room.common
    ksp libs.androidx.room.compiler
    implementation libs.androidx.room.runtime
    implementation libs.androidx.room.ktx
    implementation libs.androidx.sqlite

    implementation libs.aws.android.sdk.core
    implementation libs.aws.android.sdk.ddb
    implementation libs.aws.android.sdk.ddb.mapper

    implementation libs.dagger
    implementation libs.dagger.hilt.android
    implementation libs.dagger.hilt.core
    ksp libs.dagger.compiler
    ksp libs.dagger.hilt.android.compiler
    ksp libs.androidx.hilt.compiler

    implementation libs.androidx.hilt.navigation.compose
    implementation libs.androidx.navigation.compose
    implementation libs.androidx.navigation.common
    implementation libs.androidx.navigation.runtime

    implementation platform(libs.firebase.bom)
    implementation libs.firebase.analytics
    implementation libs.firebase.auth
    implementation libs.firebase.config
    implementation libs.firebase.crashlytics
    implementation libs.firebase.firestore

    implementation platform(libs.arrow.stack)
    implementation libs.arrow.core

    implementation libs.kotlinx.coroutines.core
    implementation libs.kotlinx.coroutines.play.services

    implementation platform(libs.androidx.compose.bom)
    implementation libs.androidx.compose.ui
    implementation libs.androidx.compose.ui.geomtery
    implementation libs.androidx.compose.ui.graphics
    implementation libs.androidx.compose.ui.text
    implementation libs.androidx.compose.runtime
    implementation libs.androidx.compose.runtime.saveable
    // Tooling support (Previews, etc.)
    implementation libs.androidx.compose.ui.tooling
    implementation libs.androidx.compose.ui.tooling.preview
    implementation libs.androidx.compose.ui.unit
    // Foundation (Border, Background, Box, Image, Scroll, shapes, animations, etc.)
    implementation libs.androidx.compose.foundation
    implementation libs.androidx.compose.foundation.layout
    // Material Design
    implementation libs.androidx.compose.material
    // Material design icons
    implementation libs.androidx.compose.material.icons.core
    implementation libs.androidx.compose.material.icons.extended
    // Integration with observables
    // Integration with activities
    implementation libs.androidx.activity.compose
    // Animations
    implementation libs.androidx.compose.animation
    implementation libs.androidx.compose.animation.core
    // Integration with ViewModels
    testImplementation libs.androidx.lifecycle.livedata.core
    implementation libs.androidx.lifecycle.viewmodel
    implementation libs.androidx.lifecycle.viewmodel.compose
    implementation libs.androidx.lifecycle.viewmodel.savedstate

    // match androidx.compose-ui version with https://github.com/google/accompanist#compose-versions
    implementation libs.accompanist.adaptive


    implementation libs.androidx.credentials

    // optional - needed for credentials support from play services, for devices running
    // Android 13 and below.
    runtimeOnly libs.androidx.credentials.play.services.auth
    implementation libs.googleid
}
