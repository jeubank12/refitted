plugins {
    id 'com.google.devtools.ksp'
    id 'com.android.application'
    id 'com.google.gms.google-services'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

android {
    defaultConfig {
        applicationId "com.litus_animae.refitted"
        minSdkVersion 26
        compileSdk 34
        targetSdkVersion 34
        versionCode 3426003
        versionName "1.2.3-SNAPSHOT"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ksp {
            arg("room.schemaLocation", "$projectDir/schemas".toString())
            arg("room.expandProjection", "true")
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += []
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            ndk { debugSymbolLevel 'FULL' }
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue 'string', "cognito_identity_pool_id", Refitted_IdentityPoolId
            resValue 'string', "firebase_id_source", Refitted_OpenIdSource
            debuggable = false
        }
        minDebug {
            signingConfig signingConfigs.debug
            minifyEnabled true
            shrinkResources true
            ndk { debugSymbolLevel 'FULL' }
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue 'string', "cognito_identity_pool_id", Refitted_IdentityPoolId
            resValue 'string', "firebase_id_source", Refitted_OpenIdSource
            debuggable true
            applicationIdSuffix ".min_debug"
            versionNameSuffix "-MIN-DEBUG"
        }
        debug {
            signingConfig signingConfigs.debug
            resValue 'string', "cognito_identity_pool_id", Refitted_IdentityPoolId
            resValue 'string', "firebase_id_source", Refitted_OpenIdSource
            applicationIdSuffix ".debug"
            versionNameSuffix "-DEBUG"
        }
    }
    buildFeatures {
        dataBinding true
        compose true
        buildConfig true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
        freeCompilerArgs += ["-opt-in=kotlin.RequiresOptIn"]
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.5.14'
    }
    namespace 'com.litus_animae.refitted'
    
    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
    }

    lint {
        checkDependencies true
    }
}

dependencies {

    implementation libs.androidx.activity
    implementation libs.androidx.annotation
    implementation libs.androidx.appcompat
    // this may be used for some sort of error reporting
    // want to replace with crashlytics
    implementation libs.androidx.legacy.support.v4
    implementation libs.guava
    implementation libs.javax.inject

    testImplementation libs.androidx.core.runtime
    implementation libs.androidx.fragment
    implementation libs.androidx.recyclerview

    def room_version = '2.6.1'
    implementation libs.androidx.room.paging
    def paging_version = '3.3.1'
    implementation libs.androidx.paging.common
    implementation libs.androidx.paging.common.ktx
    implementation "androidx.paging:paging-compose:$paging_version"
    implementation libs.androidx.paging.runtime

    // UI Tests
    androidTestImplementation platform(libs.androidx.compose.bom)
    androidTestImplementation libs.junit
    debugRuntimeOnly libs.androidx.compose.ui.test.manifest
    def junit_version = '5.10.3'
    testImplementation libs.junit.jupiter
    testImplementation libs.junit.jupiter.api
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_version"
    testImplementation libs.truth
    androidTestImplementation libs.androidx.runner
    androidTestImplementation libs.androidx.test.monitor

    implementation libs.androidx.room.common
    implementation libs.androidx.room.runtime
    ksp "androidx.room:room-compiler:$room_version"
    implementation libs.androidx.room.ktx
    implementation libs.androidx.sqlite

    implementation libs.aws.android.sdk.core
    implementation libs.aws.android.sdk.ddb
    implementation libs.aws.android.sdk.ddb.mapper

    def hilt_compiler_version = '1.2.0'
    implementation libs.dagger
    implementation libs.dagger.hilt.android
    implementation libs.dagger.hilt.core
    kapt libs.dagger.compiler
    kapt "com.google.dagger:hilt-android-compiler:$hilt_android_version"
    annotationProcessor "com.google.dagger:hilt-android-compiler:$hilt_android_version"
    ksp "androidx.hilt:hilt-compiler:$hilt_compiler_version"

    implementation libs.androidx.hilt.navigation.compose
    implementation libs.androidx.navigation.compose
    implementation libs.androidx.navigation.common
    implementation libs.androidx.navigation.runtime

    implementation platform(libs.firebase.bom)
    implementation libs.firebase.auth
    implementation libs.firebase.firestore
    implementation libs.firebase.analytics

    implementation platform(libs.arrow.stack)
    implementation libs.arrow.core

    def kotlinx_couroutines_version = '1.8.1'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinx_couroutines_version"
    implementation libs.kotlinx.coroutines.play.services

    implementation platform(libs.androidx.compose.bom)
    implementation libs.androidx.compose.ui
    implementation libs.androidx.compose.ui.geomtery
    implementation libs.androidx.compose.ui.graphics
    implementation libs.androidx.compose.ui.text
    implementation libs.androidx.compose.runtime
    implementation libs.androidx.compose.runtime.saveable
    // Tooling support (Previews, etc.)
    implementation libs.androidx.compose.ui.tooling
    implementation libs.androidx.compose.ui.tooling.preview
    implementation libs.androidx.compose.ui.unit
    // Foundation (Border, Background, Box, Image, Scroll, shapes, animations, etc.)
    implementation libs.androidx.compose.foundation
    implementation libs.androidx.compose.foundation.layout
    // Material Design
    implementation libs.androidx.compose.material
    // Material design icons
    implementation libs.androidx.compose.material.icons.core
    implementation libs.androidx.compose.material.icons.extended
    // Integration with observables
    // Integration with activities
    implementation libs.androidx.activity.compose
    // Animations
    implementation libs.androidx.compose.animation
    implementation libs.androidx.compose.animation.core
    // Integration with ViewModels
    def lifecycle_version = '2.8.4'
    testImplementation libs.androidx.lifecycle.livedata.core
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycle_version"
    implementation libs.androidx.lifecycle.viewmodel
    implementation libs.androidx.lifecycle.viewmodel.savedstate

    // match androidx.compose-ui version with https://github.com/google/accompanist#compose-versions
    implementation libs.accompanist.adaptive


    implementation libs.androidx.credentials

    // optional - needed for credentials support from play services, for devices running
    // Android 13 and below.
    runtimeOnly libs.androidx.credentials.play.services.auth
    implementation libs.googleid
}

repositories {
    mavenCentral()
}
