plugins {
    id 'com.google.devtools.ksp'
    id 'com.android.application'
    id 'com.google.gms.google-services'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

android {
    defaultConfig {
        applicationId "com.litus_animae.refitted"
        minSdkVersion 26
        compileSdk 34
        targetSdkVersion 34
        versionCode 3426001
        versionName "1.2.1-SNAPSHOT"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ksp {
            arg("room.schemaLocation", "$projectDir/schemas".toString())
            arg("room.expandProjection", "true")
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += []
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            ndk { debugSymbolLevel 'FULL' }
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue 'string', "cognito_identity_pool_id", Refitted_IdentityPoolId
            resValue 'string', "firebase_id_source", Refitted_OpenIdSource
            debuggable = false
        }
        minDebug {
            signingConfig signingConfigs.debug
            minifyEnabled true
            shrinkResources true
            ndk { debugSymbolLevel 'FULL' }
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue 'string', "cognito_identity_pool_id", Refitted_IdentityPoolId
            resValue 'string', "firebase_id_source", Refitted_OpenIdSource
            debuggable true
            applicationIdSuffix ".min_debug"
            versionNameSuffix "-MIN-DEBUG"
        }
        debug {
            signingConfig signingConfigs.debug
            resValue 'string', "cognito_identity_pool_id", Refitted_IdentityPoolId
            resValue 'string', "firebase_id_source", Refitted_OpenIdSource
            applicationIdSuffix ".debug"
            versionNameSuffix "-DEBUG"
        }
    }
    buildFeatures {
        dataBinding true
        compose true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
        freeCompilerArgs += ["-opt-in=kotlin.RequiresOptIn"]
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.5.4'
    }
    namespace 'com.litus_animae.refitted'
    
    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
    }

    lint {
        checkDependencies true
    }
}

dependencies {

    def activity_version = '1.8.1'
    implementation "androidx.activity:activity-ktx:$activity_version"
    // this may be used for some sort of error reporting
    // want to replace with crashlytics
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    def android_core_version = '2.2.0'
    implementation "androidx.arch.core:core-runtime:$android_core_version"
    implementation "androidx.arch.core:core-common:$android_core_version"
    implementation 'androidx.core:core-ktx:1.12.0'

    def room_version = '2.6.1'
    implementation "androidx.room:room-paging:$room_version"
    def paging_version = '3.2.1'
    implementation "androidx.paging:paging-common-ktx:$paging_version"
    implementation "androidx.paging:paging-runtime-ktx:$paging_version"
    implementation "androidx.paging:paging-compose:$paging_version"

    // UI Tests
    androidTestImplementation platform('androidx.compose:compose-bom:2024.05.00')
    androidTestImplementation('androidx.compose.ui:ui-test-junit4')
    debugImplementation('androidx.compose.ui:ui-test-manifest')
    def junit_version = '5.10.2'
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_version"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_version"
    testImplementation 'com.google.truth:truth:1.4.1'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    implementation "androidx.room:room-runtime:$room_version"
    ksp "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"

    def aws_android_sdk_version = '2.75.0'
    implementation "com.amazonaws:aws-android-sdk-core:$aws_android_sdk_version"
    implementation "com.amazonaws:aws-android-sdk-ddb-mapper:$aws_android_sdk_version"

    def hilt_compiler_version = '1.1.0'
    implementation "com.google.dagger:hilt-android:$hilt_android_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_android_version"
    annotationProcessor "com.google.dagger:hilt-android-compiler:$hilt_android_version"
    implementation 'androidx.navigation:navigation-compose:2.6.0'
    implementation "androidx.hilt:hilt-navigation-compose:$hilt_compiler_version"
    ksp "androidx.hilt:hilt-compiler:$hilt_compiler_version"

    implementation 'com.google.android.gms:play-services-auth:21.0.0'

    implementation platform('com.google.firebase:firebase-bom:32.8.1')
    implementation 'com.google.firebase:firebase-auth-ktx'
    implementation 'com.google.firebase:firebase-firestore-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'


    implementation platform('io.arrow-kt:arrow-stack:1.2.4')
    implementation "io.arrow-kt:arrow-core"

    implementation 'org.jetbrains.kotlinx:kotlinx-collections-immutable-jvm:0.3.7'
    def kotlinx_couroutines_version = '1.8.0'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinx_couroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-play-services:$kotlinx_couroutines_version"

    implementation platform('androidx.compose:compose-bom:2024.05.00')
    implementation "androidx.compose.ui:ui"
    // Tooling support (Previews, etc.)
    implementation "androidx.compose.ui:ui-tooling"
    // Foundation (Border, Background, Box, Image, Scroll, shapes, animations, etc.)
    implementation "androidx.compose.foundation:foundation"
    // Material Design
    implementation "androidx.compose.material:material"
    // Material design icons
    implementation "androidx.compose.material:material-icons-core"
    implementation "androidx.compose.material:material-icons-extended"
    // Integration with observables
    implementation "androidx.compose.runtime:runtime-livedata"
    // Integration with activities
    implementation "androidx.activity:activity-compose:$activity_version"
    // Animations
    implementation "androidx.compose.animation:animation"
    // Integration with ViewModels
    def lifecycle_version = '2.6.2'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycle_version"

    // match androidx.compose-ui version with https://github.com/google/accompanist#compose-versions
    implementation 'com.google.accompanist:accompanist-adaptive:0.32.0'
}

repositories {
    mavenCentral()
}
